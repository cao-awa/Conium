plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    mavenCentral()
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-neoforge:${rootProject.architectury_api_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${project.kotlin_version}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${project.kotlin_version}"

    shadowBundle "org.jetbrains.kotlin:kotlin-scripting-jvm:${project.kotlin_version}"
    shadowBundle "org.jetbrains.kotlin:kotlin-scripting-jvm-host:${project.kotlin_version}"

    implementation 'com.alibaba.fastjson2:fastjson2:2.0.50'

    implementation 'com.github.cao.awa:catheter:1.0.42'
    implementation 'com.github.cao.awa:sinuatum:1.0.15'

    implementation "com.github.cao.awa:structuring-translator:${project.structuring_translator_version}"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

kotlin {
    jvmToolchain(21)
}
